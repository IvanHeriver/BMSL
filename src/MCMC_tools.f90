module MCMC_tools

!~**********************************************************************
!~* Purpose: MCMC samplers
!~**********************************************************************
!~* Programmer: Ben Renard, University of Newcastle
!~**********************************************************************
!~* Last modified: 18/07/2008
!~**********************************************************************
!~* Comments: quite low level subroutines - basic MCMC engines
!~* 1. For fancier MCMC strategies (eg adaptive strategies) see
!~* MCMCStrategyLib
!~* 2. For more flexible and general MCMC engines see DMSL (if you're
!~* ready to face DK's madness...)
!~* 3. I tried to keep things pretty simple in this module - not a lot
!~* of options available but I trust these subs might work OK for
!~* not-too-monstruous target distributions
!~* 4. works in log-space (ie the log-posterior is interfaced)
!~**********************************************************************
!~* References:
!~**********************************************************************
!~* 2Do List:
!~**********************************************************************
!~* Quick description of public procedures:
!~*     1. Metro_GaussJump:standard Metropolis with gaussian Jump
!~*     2. Metro_OAAT_GaussJump: the One-At-A-Time block Metropolis
!~*        sampler with univariate Gaussian jump proposal
!~*     Left for future work:
!~*     3. MH: standard Metropolis-Hastings algorithm. Rely on the (not
!~*        yet existing) MultivariateDistributionLib for being able to
!~*        generate a candidate from an arbitrary multivariate proposal.
!~*     4. MH_OAAT: the One-At-A-Time block Metropolis-Hastings sampler.
!~*        Jump distribution might be different for each component
!~**********************************************************************

use kinds_dmsl_kit ! numeric kind definitions from DMSL

implicit none
Private
public :: Metro_GaussJump, Metro_OAAT_GaussJump,Metro_OAAT_GaussJump_v2

Contains

!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

subroutine Metro_GaussJump(f,x,fx,fAux,covar,move,err,mess)

!^**********************************************************************
!^* Purpose: Single metropolis update
!^**********************************************************************
!^* Programmer: Ben Renard, University of Newcastle
!^**********************************************************************
!^* Last modified: 18/07/2008
!^**********************************************************************
!^* Comments: Note that covar is choleskized outside of this sub
!^**********************************************************************
!^* References:
!^**********************************************************************
!^* 2Do List:
!^**********************************************************************
!^* IN
!^*     1.f:log-density of the target distribution (see interface)
!^*     2.covar, Choleskized covariance matrix of Gaussian proposal
!^* OUT
!^*     1.move, did the chain move?
!^*     2.err, error code; <0:Warning, ==0:OK, >0: Error
!^*     3.mess, error message
!^* INOUT
!^*     1.x, current x (in) - new x(out)
!^*     2.fx, f(x)
!^*     3.[fAux], auxiliaries: any other useful value generated by f (eg prior, LL)
!^**********************************************************************

use numerix_dmsl_kit,only:normaldev, uniran

interface
    subroutine f(x,feas, isnull,fx,fAux,err,mess)
        use kinds_dmsl_kit
        real(mrk),intent(in)::x(:)
        logical,intent(out)::feas,isnull
        real(mrk),intent(out)::fx
        real(mrk),intent(out),optional::fAux(:)
        integer(mik),intent(out)::err
        character(*),intent(out)::mess
    end subroutine f
end interface

real(mrk),intent(in)::covar(:,:)
real(mrk),intent(inout)::x(:),fx
real(mrk),intent(inout),optional::fAux(:)
logical,intent(out)::move
integer(mik), intent(out)::err
character(*),intent(out)::mess
!locals
real(mrk)::candid(size(x)),fcandid
real(mrk),allocatable::fauxOld(:)
real(mrk)::uran,ratio
real(mrk)::cov(size(covar,dim=1),size(covar,dim=2))
logical::feas,isnull

err=0;mess='';move=.false.
if(present(fAux)) then
    if(allocated(fauxOld)) deallocate(fauxOld)
    allocate(fauxOld(size(faux)))
    fauxOld=faux
endif

!candidate
cov=covar
call normaldev(mean=x, do_dcmp=.false., covar_dcmp=cov,&
               gdev=candid, err=err, message=mess)
If(err>0) then
    mess='Metro_GaussJump:'//trim(mess);return
endif
!evaluate candidate
call f(candid,feas,isnull,fcandid,fAux,err,mess)
If(err>0) then
    mess='Metro_GaussJump:'//trim(mess);return
endif
if((.not.feas).or.(isnull)) then
    if(present(fAux)) fAux=fAuxOld
    move=.false.;return
endif

!apply Metropolis acceptance rule
call uniran(uran)
ratio=exp(min(max(-100.0_mrk, fcandid-fx), 0.0_mrk))
if(uran<=ratio) then !accept candidate
    x=candid;fx=fcandid;move=.true.
else ! don't move!
    move=.false.
    if(present(fAux)) fAux=fAuxOld
endif

end subroutine Metro_GaussJump

!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

subroutine Metro_OAAT_GaussJump(f,x,fx,fAux,stdev,move,err,mess)

!^**********************************************************************
!^* Purpose: One-at-a-time block metropolis update
!^**********************************************************************
!^* Programmer: Ben Renard, University of Newcastle
!^**********************************************************************
!^* Last modified: 07/08/2008
!^**********************************************************************
!^* Comments:
!^**********************************************************************
!^* References:
!^**********************************************************************
!^* 2Do List:
!^**********************************************************************
!^* IN
!^*     1.f:log-density of the target distribution (see interface)
!^*     2.stdev, vector of stdev of the 1-D gaussian jump distrib.
!^* OUT
!^*     1.move, logical vector: did each component move?
!^*     2.err, error code; <0:Warning, ==0:OK, >0: Error
!^*     3.mess, error message
!^* INOUT
!^*     1.x, current x (in) - new x(out)
!^*     2.fx, f(x)
!^*     3.[fAux], auxiliaries: any other useful value generated by f (eg prior, LL)
!^**********************************************************************

use numerix_dmsl_kit,only:normaldev, uniran

interface
    subroutine f(x,feas,isnull,fx,fAux,err,mess)
        use kinds_dmsl_kit
        real(mrk),intent(in)::x(:)
        logical,intent(out)::feas,isnull
        real(mrk),intent(out)::fx
        real(mrk),intent(out),optional::fAux(:)
        integer(mik),intent(out)::err
        character(*),intent(out)::mess
    end subroutine f
end interface

real(mrk),intent(in)::stdev(:)
real(mrk),intent(inout)::x(:),fx
real(mrk),intent(inout),optional::fAux(:)
logical,intent(out)::move(:)
integer(mik), intent(out)::err
character(*),intent(out)::mess
!locals
real(mrk)::candid(size(x)),fcandid
real(mrk),allocatable::fauxOld(:)
real(mrk)::uran,ratio,dev
logical::feas,isnull
integer(mik)::i,n

err=0;mess='';move=.false.
if(present(fAux)) then
    if(allocated(fauxOld)) deallocate(fauxOld)
    allocate(fauxOld(size(faux)))
endif
n=size(x)

ComponentLoop: do i=1,n ! loop on each component
    if(present(faux)) fauxOld=faux !init
    ! candidate
    call normaldev(mean=x(i), sdev=stdev(i),&
                   gdev=dev, err=err, message=mess)
    If(err>0) then
        mess='Metro_OAAT_GaussJump:'//trim(mess);return
    endif
    candid=x;candid(i)=dev
    !evaluate candidate
    call f(candid,feas,isnull,fcandid,fAux,err,mess)
    If(err>0) then
        mess='Metro_OAAT_GaussJump:'//trim(mess);return
    endif
    if((.not.feas).or.isnull) then
        if(present(fAux)) fAux=fAuxOld
        move(i)=.false.;cycle ComponentLoop
    endif

    !apply Metropolis acceptance rule
    call uniran(uran)
    ratio=exp(min(max(-100.0_mrk, fcandid-fx), 0.0_mrk))
    if(uran<=ratio) then !accept candidate
        x(i)=candid(i);fx=fcandid;move(i)=.true.
    else ! don't move!
        move(i)=.false.
        if(present(fAux)) fAux=fAuxOld
    endif
enddo ComponentLoop

end subroutine Metro_OAAT_GaussJump

!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
subroutine Metro_OAAT_GaussJump_v2(f,x,fx,fAux,stdev,move,err,mess)

!^**********************************************************************
!^* Purpose: One-at-a-time block metropolis update
!^**********************************************************************
!^* Programmer: Ben Renard, Irstea Lyon / coded @ Columbia Uni
!^**********************************************************************
!^* Last modified: 19/11/2012
!^**********************************************************************
!^* Comments: This is exatly the same as Metro_OAAT_GaussJump but
!^*           with a different interface to f.
!^*           The current component is passed to the function f. The
!^*           objective is to enable fast computations within f since
!^*           in some cases (hierachical in particular) there is no need
!^*           to compute the full posterior because many parts cancel in
!^*           the Metropolis ratio
!^**********************************************************************
!^* References:
!^**********************************************************************
!^* 2Do List:
!^**********************************************************************
!^* IN
!^*     1.f:log-density of the target distribution (see interface)
!^*     2.stdev, vector of stdev of the 1-D gaussian jump distrib.
!^* OUT
!^*     1.move, logical vector: did each component move?
!^*     2.err, error code; <0:Warning, ==0:OK, >0: Error
!^*     3.mess, error message
!^* INOUT
!^*     1.x, current x (in) - new x(out)
!^*     2.fx, f(x)
!^*     3.[fAux], auxiliaries: any other useful value generated by f (eg prior, LL)
!^**********************************************************************

use numerix_dmsl_kit,only:normaldev, uniran

interface
    subroutine f(x,k,feas,isnull,fx,fAux,err,mess)
        use kinds_dmsl_kit
        real(mrk),intent(in)::x(:)
        integer(mik),intent(in)::k ! current component being sampled - if 0 compute full f
        logical,intent(out)::feas,isnull
        real(mrk),intent(out)::fx
        real(mrk),intent(out),optional::fAux(:)
        integer(mik),intent(out)::err
        character(*),intent(out)::mess
    end subroutine f
end interface

real(mrk),intent(in)::stdev(:)
real(mrk),intent(inout)::x(:),fx
real(mrk),intent(inout),optional::fAux(:)
logical,intent(out)::move(:)
integer(mik), intent(out)::err
character(*),intent(out)::mess
!locals
real(mrk)::candid(size(x)),fcandid
real(mrk),allocatable::fauxOld(:)
real(mrk)::uran,ratio,dev
logical::feas,isnull
integer(mik)::i,n

err=0;mess='';move=.false.
if(present(fAux)) then
    if(allocated(fauxOld)) deallocate(fauxOld)
    allocate(fauxOld(size(faux)))
endif
n=size(x)

ComponentLoop: do i=1,n ! loop on each component
    if(present(faux)) fauxOld=faux !init
    ! candidate
    call normaldev(mean=x(i), sdev=stdev(i),&
                   gdev=dev, err=err, message=mess)
    If(err>0) then
        mess='Metro_OAAT_GaussJump:'//trim(mess);return
    endif
    candid=x;candid(i)=dev
    !evaluate candidate
    call f(candid,i,feas,isnull,fcandid,fAux,err,mess)
    If(err>0) then
        mess='Metro_OAAT_GaussJump:'//trim(mess);return
    endif
    if((.not.feas).or.isnull) then
        if(present(fAux)) fAux=fAuxOld
        move(i)=.false.;cycle ComponentLoop
    endif

    !apply Metropolis acceptance rule
    call uniran(uran)
    ratio=exp(min(max(-100.0_mrk, fcandid-fx), 0.0_mrk))
    if(uran<=ratio) then !accept candidate
        x(i)=candid(i);fx=fcandid;move(i)=.true.
    else ! don't move!
        move(i)=.false.
        if(present(fAux)) fAux=fAuxOld
    endif
enddo ComponentLoop

end subroutine Metro_OAAT_GaussJump_v2
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

end module MCMC_tools
